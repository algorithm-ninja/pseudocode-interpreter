// Row major
type Mat: (integer, integer, integer, integer)

function I() -> Mat
    return (1, 0, 0, 1)
end function

function mat_mul(a: Mat, b: Mat) -> Mat
    variable c11: integer <- a.0 * b.0 + a.1 * b.2
    variable c12: integer <- a.0 * b.1 + a.1 * b.3
    variable c21: integer <- a.2 * b.0 + a.3 * b.2
    variable c22: integer <- a.2 * b.1 + a.3 * b.3
    return (c11, c12, c21, c22)
end function

function mat_pow(mat: Mat, n: integer) -> Mat
    variable res: Mat <- I()

    while n > 0 do
        if (n mod 2) != 0 then
            res <- mat_mul(res, mat)
        end if

        mat <- mat_mul(mat, mat)
        n <- n / 2
    end while

    return res
end function

function main()
    for n in [0...100) do
        variable Fn: integer <- mat_pow((1, 1, 1, 0), n).2
        output(Fn)
    end for
end function
